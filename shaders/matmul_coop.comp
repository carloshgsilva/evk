#include "evk.frag"

// Coopmat version of matmul.comp
// Doesn't have bounds check, assumes 64 aligned dimensions

CONSTANT(0) uint K = 0;
CONSTANT(1) uint N = 0;
CONSTANT(2) uint TRANSPOSE_A = 0;
CONSTANT(3) uint SUM_C = 0;
CONSTANT(4) uint TILE_M = 0;
CONSTANT(5) uint TILE_N = 0;

#extension GL_EXT_shader_explicit_arithmetic_types_float16 : enable
#extension GL_EXT_shader_16bit_storage : enable
#extension GL_KHR_cooperative_matrix : enable
#extension GL_KHR_memory_scope_semantics : enable
#extension GL_EXT_control_flow_attributes : enable
#extension GL_EXT_buffer_reference : enable

layout(buffer_reference) buffer BufferFp16 { float16_t x[]; };

PUSH(
    BufferFp16 inputA;
    BufferFp16 inputB;
    BufferFp16 inputC;
)

#define TILE_K 16
// #define TILE_M 80
// #define TILE_N 80
#define SUB_TILE 16

const uint C_ROWS = TILE_M / SUB_TILE;
const uint C_COLS = TILE_N / SUB_TILE;
coopmat<float16_t, gl_ScopeSubgroup, SUB_TILE, SUB_TILE, gl_MatrixUseAccumulator> result[C_ROWS][C_COLS];

uint coordToOffset(uint i, uint j, uint stride, bool colMajor) {
    return colMajor ? (stride * j + i) : (stride * i + j);
}

COMPUTE(32, 1, 1)
void main() {
    uvec2 tileID = uvec2(gl_WorkGroupID.xy);

    // Initialize result to zero
    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            result[i][j] = coopmat<float16_t, gl_ScopeSubgroup, SUB_TILE, SUB_TILE, gl_MatrixUseAccumulator>(0.0);
        }
    }

    // On each iteration, load a row of cooperative matrices from matrix A,
    // load a column of cooperative matrices from matrix B, and multiply all
    // pairs of those matrices.
    for (uint chunkK = 0; chunkK < K; chunkK += TILE_K) {
        coopmat<float16_t, gl_ScopeSubgroup, SUB_TILE, SUB_TILE, gl_MatrixUseA> matA[C_ROWS];
        [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
            uint gi = TILE_M * tileID.y + SUB_TILE * i;
            uint gk = chunkK;
            coopMatLoad(matA[i], inputA.x, coordToOffset(gi, gk, K, false), K, gl_CooperativeMatrixLayoutRowMajor);
        }
        coopmat<float16_t, gl_ScopeSubgroup, SUB_TILE, SUB_TILE, gl_MatrixUseB> matB;
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            uint gj = TILE_N * tileID.x + SUB_TILE * j;
            uint gk = chunkK;
            coopMatLoad(matB, inputB.x, coordToOffset(gk, gj, N, false), N, gl_CooperativeMatrixLayoutRowMajor);
            [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
                result[i][j] = coopMatMulAdd(matA[i], matB, result[i][j]);
            }
        }
    }

    // Store the result
    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            uint gi = TILE_M * tileID.y + SUB_TILE * i;
            uint gj = TILE_N * tileID.x + SUB_TILE * j;
            coopMatStore(result[i][j], inputC.x, coordToOffset(gj, gi, N, false), N, gl_CooperativeMatrixLayoutRowMajor);
        }
    }
}